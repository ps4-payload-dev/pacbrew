pkgname=ps4-payload-zlib
pkgver=1.3.1
pkgrel=1
pkgdesc='Deflate compression method library'
arch=('any')
url='http://www.zlib.net/'
license=('zlib')
options=(!strip libtool staticlibs)
source=("https://zlib.net/zlib-$pkgver.tar.gz")
sha256sums=('9a93b2b7dfdac77ceba5a558a580e74667dd6fede4585b91eefb60f03b72df23')
groups=('ps4-payload-dev')
makedepends=('ps4-payload-sdk')

prepare() {
    if [ -z "${PS4_PAYLOAD_SDK}" ]; then
	export PS4_PAYLOAD_SDK=/opt/ps4-payload-sdk
    fi

    cd zlib-$pkgver/contrib/minizip
    autoreconf --force --verbose --install
}

build() {
    source "${PS4_PAYLOAD_SDK}/toolchain/orbis.sh"
    cd zlib-$pkgver

    ./configure --prefix="${PREFIX}"
    ${MAKE} CFLAGS=-Os

    # libminizip
    cd contrib/minizip
    ./configure --prefix="${PREFIX}" --host=x86_64
    ${MAKE}
}

package() {
    source "${PS4_PAYLOAD_SDK}/toolchain/orbis.sh"
    cd zlib-$pkgver

    export DESTDIR="${pkgdir}/${DESTDIR}"
    ${MAKE} install

    cd contrib/minizip
    ${MAKE} install

    ${PS4_CROSS_FIX_ROOT} "${DESTDIR}/${PREFIX}"
}
