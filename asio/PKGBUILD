pkgname=ps4-payload-asio
pkgver=1.34.2
pkgrel=1
pkgdesc='Cross-platform C++ library for ASynchronous network I/O'
url='https://think-async.com/Asio/'
arch=('any')
license=('BSL-1.0')
source=("https://github.com/chriskohlhoff/asio/archive/refs/tags/asio-${pkgver//./-}.tar.gz")
sha256sums=('f3bac015305fbb700545bd2959fbc52d75a1ec2e05f9c7f695801273ceb78cf5')
options=(!strip libtool staticlibs)
groups=('ps4-payload-dev')
makedepends=('ps4-payload-sdk')

prepare() {
    if [ -z "${PS4_PAYLOAD_SDK}" ]; then
	export PS4_PAYLOAD_SDK=/opt/ps4-payload-sdk
    fi

    cd "asio-asio-${pkgver//./-}/asio"
    autoreconf -fiv
}

build() {
    source "${PS4_PAYLOAD_SDK}/toolchain/orbis.sh"
    cd "asio-asio-${pkgver//./-}/asio"

    ./configure --prefix="${PREFIX}" --host=x86_64-pc-freebsd \
		--enable-static --disable-shared
    ${MAKE}
}


package() {
    source "${PS4_PAYLOAD_SDK}/toolchain/orbis.sh"
    cd "asio-asio-${pkgver//./-}/asio"

    export DESTDIR="${pkgdir}/${DESTDIR}"
    ${MAKE} install

    ${PS4_CROSS_FIX_ROOT} "${DESTDIR}/${PREFIX}"
}
