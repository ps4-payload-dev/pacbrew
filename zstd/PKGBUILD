pkgname=ps4-payload-zstd
pkgver=1.5.6
pkgrel=1
pkgdesc='Zstandard - Fast real-time compression algorithm'
url='https://facebook.github.io/zstd/'
arch=('any')
license=(BSD-3-Clause GPL-2.0-only)
options=(!strip libtool staticlibs)
source=(https://github.com/facebook/zstd/releases/download/v${pkgver}/zstd-${pkgver}.tar.gz)
sha256sums=('8c29e06cf42aacc1eafc4077ae2ec6c6fcb96a626157e0593d5e82a34fd403c1')
groups=('ps4-payload-dev')
makedepends=('ps4-payload-sdk')

prepare() {
    if [ -z "${PS4_PAYLOAD_SDK}" ]; then
	export PS4_PAYLOAD_SDK=/opt/ps4-payload-sdk
    fi
}

build() {
    source "${PS4_PAYLOAD_SDK}/toolchain/orbis.sh"
    cd zstd-${pkgver}

    ${CMAKE} -DCMAKE_BUILD_TYPE=Release \
	     -DZSTD_BUILD_STATIC=YES \
	     -DZSTD_BUILD_SHARED=NO \
	     -B builddir \
             -S build/cmake
    ${MAKE} -C builddir
}

package() {
    source "${PS4_PAYLOAD_SDK}/toolchain/orbis.sh"
    cd zstd-${pkgver}

    export DESTDIR="${pkgdir}/${DESTDIR}"
    ${MAKE} -C builddir install

    ${PS4_CROSS_FIX_ROOT} "${DESTDIR}/${PREFIX}"
}
