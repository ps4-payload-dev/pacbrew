pkgname=ps4-payload-xz
pkgver=5.4.6
pkgrel=1
pkgdesc='Library and command line tools for XZ and LZMA compressed files'
arch=('any')
url='https://github.com/tukaani-project/xz'
license=('GPL' 'LGPL' 'custom')
options=(!strip libtool staticlibs)
source=("https://github.com/tukaani-project/xz/releases/download/v${pkgver}/xz-${pkgver}.tar.xz")
sha256sums=('b92d4e3a438affcf13362a1305cd9d94ed47ddda22e456a42791e630a5644f5c')
groups=('ps4-payload-dev')
makedepends=('ps4-payload-sdk')


prepare() {
    if [ -z "${PS4_PAYLOAD_SDK}" ]; then
	export PS4_PAYLOAD_SDK=/opt/ps4-payload-sdk
    fi

    cd xz-$pkgver
    ./autogen.sh
}

build() {
    source "${PS4_PAYLOAD_SDK}/toolchain/orbis.sh"
    cd xz-$pkgver

    ./configure --prefix="${PREFIX}" --host=x86_64-pc-freebsd \
		--enable-static --disable-shared \
		--disable-nls --disable-rpath \
		--disable-scripts
    ${MAKE}
}

package() {
    source "${PS4_PAYLOAD_SDK}/toolchain/orbis.sh"
    cd xz-$pkgver

    export DESTDIR="${pkgdir}/${DESTDIR}"
    make install

    ${PS4_CROSS_FIX_ROOT} "${DESTDIR}/${PREFIX}"
}
